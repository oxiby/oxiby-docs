use std.io read_line

enum Player {
    X,
    O,
}

enum Winner {
    Player(Player),
    Draw,
    NoneYet,
}

struct Game {
    player: Player,
    rows: List<List<Option<Player>>>,

    fn winner(self) -> Winner {
        for row in (0..=2) {
            match (self.rows[row][0], self.rows[row][1], self.rows[row][2]) {
                (Some(a), Some(b), Some(c)) -> {
                    if a == b && b == c {
                        return Winner.Player(a)
                    }
                },
                _ -> (),
            }
        }

        for column in (0..=2) {
            match (self.rows[0][column], self.rows[1][column], self.rows[2][column]) {
                (Some(a), Some(b), Some(c)) -> {
                    if a == b && b == c {
                        return Winner.Player(a)
                    }
                },
                _ -> (),
            }
        }

        match (self.rows[0][0], self.rows[1][1], self.rows[2][2]) {
            (Some(a), Some(b), Some(c)) -> {
                if a == b && b == c {
                    return Winner.Player(a)
                }
            },
            _ -> (),
        }

        match (self.rows[0][2], self.rows[1][1], self.rows[2][0]) {
            (Some(a), Some(b), Some(c)) -> {
                if a == b && b == c {
                    return Winner.Player(a)
                }
            },
            _ -> (),
        }

        for row in self.rows {
            for column in row {
                match column {
                    Some(player) -> (),
                    None -> return Winner.NoneYet,
                }
            }
        }

        Winner.Draw
    }

    fn choose(self, cell: Integer, player: Player) -> Boolean {
        if !(1..=9).contains(cell) {
            return false
        }

        match self.rows[(cell - 1) / 3][(cell - 1) % 3] {
            Some(_) -> false,
            None -> {
                self.rows[(cell - 1) / 3][(cell - 1) % 3] = Some(player)
                true
            }
        }
    }
}

fn main() {
    let game = Game {
        player: Player.X,
        rows: List.times(3, fn () { List.times(3, fn () { None }) }),
    }

    loop {
        print_line("Game board:")

        for row in game.rows {
            for cell in row {
                match cell {
                    Some(player) -> print(player),
                    None -> print("."),
                }
            }

            print_line("")
        }

        print_line("")

        match game.winner() {
            Winner.Player(player) -> {
                print_line("The winner is #{player}!")
                break
            },
            Winner.Draw -> {
                print_line("It's a draw!")
                break
            },
            Winner.NoneYet -> (),
        }

        loop {
            print("It's #{game.player}'s turn. Enter the cell to play (1-9): ")

            match read_line().map(fn (s) { s.to_i() }) {
                Ok(cell) -> {
                    if game.choose(cell, game.player) {
                        game.player = match game.player {
                            Player.X -> Player.O,
                            Player.O -> Player.X,
                        }

                        break
                    }
                },
                _ -> (),
            }

            print_line("")
            print_line("You entered an invalid cell number.")
        }

        print_line("")
    }
}
